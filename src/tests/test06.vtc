varnishtest "Dropping polling of a backend with an out-of-band probe address (cf. c00035.vtc)"

server s1 {
	rxreq
	expect req.url == /foo
	txresp -bodylen 4
} -start

server s2 -repeat 40 {
	rxreq
	txresp
} -start

varnish v1 -vcl {
	import oob_probe from "${vmod_topbuild}/src/.libs/libvmod_oob_probe.so";

	probe p {
		.window = 8;
		.initial = 7;
		.threshold = 8;
		.interval = 0.1s;
	}
	backend s1 {
		.host = "${s1_addr}";
		.port = "${s1_port}";
	}

	sub vcl_init {
		oob_probe.addr(s1, p, "${s2_addr}", "${s2_port}");
	}
} -start

delay 1

varnish v1 -vcl {
	import oob_probe from "${vmod_topbuild}/src/.libs/libvmod_oob_probe.so";

	probe p {
		.window = 8;
		.initial = 7;
		.threshold = 8;
		.interval = 0.1s;
	}
	backend s1 {
		.host = "${s1_addr}";
		.port = "${s1_port}";
	}

	sub vcl_init {
		oob_probe.addr(s1, p, "${s2_addr}", "${s2_port}");
	}
} -cliok "vcl.use vcl2" -cliok "vcl.discard vcl1"

delay 1

varnish v1 -cliok "vcl.list"
varnish v1 -cliok "backend.list -p"

client c1 {
	txreq -url /foo
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 4
} -run
